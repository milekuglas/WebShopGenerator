ProjectModel:
    types*=SimpleType
    project=Project
    base_product=Product
    products*=Product     
    categories*=Category  
    enums*=Enum
;

Project:
    'project' name=ID '{'
        (package=Package
        database=Database
        build=BuildTool
        jwt=JWTSecret)#
    '}'
;

Package:
    'package:' name=/[a-z][a-z0-9_]*(\.[a-z0-9_]+)+[0-9a-z_]/
;

Database:
    'database:' name=DatabaseType
;

DatabaseType:
    'H2' | 'MYSQL' | 'PostgreSQL'
;

BuildTool:
    'build_tool:' name=BuildType
;

JWTSecret:
    'jwt_secret:' name=/[a-zA-Z0-9]*/
;

BuildType:
    'SBT'
;

Product:
    'product' name=ID type=ProductType '{'
        properties+=Property 
    '}'
;

ProductType:
    'base'|'inherited'
;

Category:
    'category' name=ID ('->' subcategory=[Category])?
;

Enum:
    'enum' name=ID '{'
        values*=ID[',']
    '}'
;

Property:
    name=ID ':' type=[Type] (validations+=Validation)?
;

Validation:
    Id | Required | NotNull | Max | Min | Size
;

Id:
    name='id'
;

Required:
    name='required'
;

NotNull:
    name='not_null'
;

Max:
    'max' '(' value=INT ')'
;

Min:
    'min' '(' value=INT ')'
;

Size:
    'size' '(' min=INT ','  max=INT ')'
;

Type:
    SimpleType | Enum
;

SimpleType:
    'type' name=ID
;









